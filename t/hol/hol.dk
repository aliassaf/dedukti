;; Types

htype: Type.
o: htype.
arrow: htype -> htype -> htype.


;; Terms

hterm: htype -> Type.
Lam: a: htype -> b: htype -> (hterm a -> hterm b) -> hterm (arrow a b).
App: a: htype -> b: htype -> hterm (arrow a b) -> hterm a -> hterm b.
Imp: hterm (arrow o (arrow o o)).
Forall: a: htype -> hterm (arrow (arrow a o) o).
Eq: a: htype -> hterm (arrow a (arrow a o)).


;; Î²-rule

[a: htype,
 b: htype,
 f: hterm a -> hterm b,
 x: hterm a] App a b (Lam a b f) x --> f x.


;; Facilities

himp: hterm o -> hterm o -> hterm o.
[a: hterm o,
 b: hterm o] App o o (App o (arrow o o) Imp a) b --> himp a b.


hforall: a: htype -> hterm (arrow a o) -> hterm o.
[a: htype,
 P: hterm (arrow a o)] App (arrow a o) o (Forall a) P --> hforall a P.


heq: a: htype -> hterm a -> hterm a -> hterm o.
[a: htype,
 x: hterm a,
 y: hterm a] App a o (App a (arrow a o) (Eq a) x) y --> heq a x y.


hequiv: hterm o -> hterm o -> hterm o.
[] heq o --> hequiv.


;; eps

eps: hterm o -> Type.

[a: hterm o,
 b: hterm o] eps (himp a b) --> eps a -> eps b.

[a: htype,
 f: hterm a -> hterm o] eps (hforall a (Lam a o f)) --> b: hterm a -> eps (f b).

[a: htype,
 x: hterm a,
 y: hterm a] eps (heq a x y) --> P: hterm (arrow a o) -> eps (App a o P x) -> eps (App a o P y).


;; Extensionality

fun_ext: a: htype -> b: htype -> f: hterm (arrow a b) -> g: hterm (arrow a b) ->
    (x: hterm a -> eps (heq b (App a b f x) (App a b g x))) -> eps (heq (arrow a b) f g).

prop_ext: p: hterm o -> q: hterm o -> eps (himp p q) -> eps (himp q p) -> eps (hequiv p q).


;; Rules

refl: a: htype -> x: hterm a -> eps (heq a x x).
[a: htype,
 x: hterm a] refl a x --> P: hterm (arrow a o) => h: eps (App a o P x) => h.


sym: a: htype -> x: hterm a -> y: hterm a -> eps (heq a x y) -> eps (heq a y x).
[a: htype,
 x: hterm a,
 y: hterm a,
 Hxy: eps (heq a x y)]
sym a x y Hxy --> P: hterm (arrow a o) => Hy: eps (App a o P y) => Hxy (Lam a o (z: hterm a => himp (App a o P z) (App a o P x))) (H: eps (App a o P x) => H) Hy.


trans: a: htype -> s: hterm a -> t: hterm a -> u: hterm a -> eps (heq a s t) -> eps (heq a t u) -> eps (heq a s u).
[a: htype,
 s: hterm a,
 t: hterm a,
 u: hterm a,
 Hst: P: hterm (arrow a o) -> eps (App a o P s) -> eps (App a o P t),
 Htu: P: hterm (arrow a o) -> eps (App a o P t) -> eps (App a o P u)]
trans a s t u Hst Htu --> P: hterm (arrow a o) => Hs: eps (App a o P s) => Htu P (Hst P Hs).


beta: a: htype -> b: htype -> f: (hterm a -> hterm b) -> u: hterm a -> eps (heq b (App a b (Lam a b f) u) (f u)).
[a: htype,
 b: htype,
 f: hterm a -> hterm b,
 u: hterm a]
beta a b f u --> P: hterm (arrow b o) => H: eps (App b o P (f u)) => H.


mk_comb: a: htype -> b: htype -> s: hterm (arrow a b) -> t: hterm (arrow a b) -> u: hterm a -> v: hterm a ->
  eps (heq (arrow a b) s t) -> eps (heq a u v) -> eps (heq b (App a b s u) (App a b t v)).
[a: htype,
 b: htype,
 s: hterm (arrow a b),
 t: hterm (arrow a b),
 u: hterm a,
 v: hterm a,
 Hst: eps (heq (arrow a b) s t),
 Huv: eps (heq a u v)]
mk_comb a b s t u v Hst Huv --> P: hterm (arrow b o) => Hsu: eps (App b o P (App a b s u)) => Hst (Lam (arrow a b) o (s: hterm (arrow a b) => App b o P (App a b s v))) (Huv (Lam a o (u: hterm a => App b o P (App a b s u))) Hsu).


eq_mp : p: hterm o -> q: hterm o -> eps (hequiv p q) -> eps p -> eps q.
[p: hterm o,
 q: hterm o,
 Hpq: eps (hequiv p q), ; P: hterm (arrow o o) -> eps (App o o P p) -> eps (App o o P q)
 Hp: eps p]
eq_mp p q Hpq Hp --> Hpq (Lam o o (r: hterm o => r)) Hp.



;; Abs (ABS) cannot be written (meta-rule), we can only write instances
;; of it

;; Impas (IMP_ANTISYM_RULE) is exactly prop_ext
